buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

sourceCompatibility = 1.8
if(JavaVersion.current().isJava9Compatible()) {
    allprojects {
        tasks.withType(JavaCompile) {
            options.release.set(8)
        }
    }
}

def projectName = 'anim8-gdx'
version "$VERSION_NAME"
group 'com.github.tommyettinger'

//api "com.github.tommyettinger:anim8-gdx:0.3.12"

//def gdxVersion = '1.10.0'
def gdxVersion = '1.9.11'

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'ISO-8859-1'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            // The -quiet is because of some sort of weird JDK JavaCompiler bug:
            // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
            options.addStringOption('Xdoclint:none,-missing', '-quiet')
        }
    }
}

javadoc.destinationDir = file('docs/apidocs')

//sourceSets.test.resources.srcDirs += [ file('src/test/resources').path ]

jar {
    archiveBaseName.set(projectName)
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  api "com.badlogicgames.gdx:gdx:$gdxVersion"
  testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}
